#!/usr/bin/env perl

use Modern::Perl '2018';
use DBI;
use Excel::Writer::XLSX;
use POSIX 'strftime';

my $SOURCE = 'responses.csv';
my $OUTFILE = '2019-2020 Parent Roster.xlsx';
my $DBFILE = 'sjsmusic.db';
my $dbh = DBI->connect("dbi:SQLite:dbname=$DBFILE",'','', { RaiseError => 1 });

my $wb = Excel::Writer::XLSX->new($OUTFILE);
my $norm = $wb->add_format( font => 'Arial', size => 10 );
my $bold = $wb->add_format( font => 'Arial', size => 10, bold => 1 );
my $url = $wb->get_default_url_format();
my $ws = $wb->add_worksheet();

my $x = 0; my $y = 0;

$ws->write($x, $y, '2019-2020 Parent Roster', $bold);
$y = 3;
$ws->write($x, $y, strftime('Updated at %I:%M%P on %A, %B %e, %Y', localtime()), $norm);
$x = 2; $y = 0;
$ws->write($x, $y, ['First Name', 'Last Name', 'Phone', 'Email' ], $bold);

sub ce {
    return '' unless defined $_[0];
    return $_[0];
}

sub make_mailto {
    my $addr = $_[0];

    return '' unless $addr;
    return '=HYPERLINK("mailto:' . $addr . '", "Send Email")';
}

sub make_email_text {
    my ($first, $last, $email) = @_;

    return '' unless $email;
    return $first . ' ' . $last . ' <' . $email . '>';
}

my $parent_sth = $dbh->prepare(qq{
    select firstname, lastname, phone, email
    from parent_roster order by lastname, firstname;
});
$parent_sth->execute();

while (my $parent = $parent_sth->fetch()) {
    my @p = $parent->@*;

    my $email = pop @p;

    push @p, make_email_text($p[0], $p[1], $email);
    push @p, make_mailto($p[-1]);

    $x++;
    $y = $#p;
    $ws->write($x, $y, pop @p, $url);
    $ws->write($x, 0,  [ map {ce($_)} @p ], $norm);
}
